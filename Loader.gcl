gcl0x
{-----------------------------------------------------------------------+
|                                                                       |
|       Loading of programs over the serial port (with Arduino)         |
|                                                                       |
+-----------------------------------------------------------------------}

{ Setup }

[def {PrintChar}
  Char=
  {Map ASCII code to offset in font table}
  82- [if<0 50+ i= \font32up
       else     i= \font82up] fontData= {Select low or high page}
  i i+ tmp= tmp+ i+    {Multiply by 5}
  fontData+ fontData=  {Add to page address to reach bitmap data for Char}
  $800 Pos+ q=           {Where to stop the inner drawing loop}
  {Draw 5 vertical slices}
  5 [do
    i=
    fontData 0? bits= fontData<++
    Pos p=
    {Draw vertical slice}
    [do
      bits 128& [if=0 32 {Blue} else 15] p.
      bits bits+ bits= {Shift left}
      p>++
      p q- if<0loop]
    Pos<++
    i 1- if>0loop]
  Pos<++ {Skip one slice for spacing}
  ret
] PrintChar=

{ Display ready to load text }
$500c Pos=
[def {Ready to load}
  $52# $65# $61# $64# $79# $20# $74# $6f#
  $20# $6c# $6f# $61# $64# 0# ] Text=
[do Text, if<>0 Text<++ PrintChar! loop]

{ Put these in variable to save some code space below }
\SYS_NextByteIn_30      NextByteIn_30=
\SYS_ConditionalCopy_34 ConditionalCopy_34=

0 \sysArgs3.            {Clears the copy counter}
$5a0c A=                {Activity indicator}
$5b0c B=                {Buffer}
103 {'g'} \sysArgs2.    {Reset checksum}

{-----------------------------------------------------------------------+
|}$590c \vLR= ret{      Inside screen area                              |
+-----------------------------------------------------------------------}
$590c:

{ Loop }

[do
  {White indicator}
  63 A.

  {Receive the next transmission frame}
  B         \sysArgs0=                                {Input buffer}
  [do \videoY, 205^ if<>0loop] NextByteIn_30      30@ {Protocol byte}
  [do \videoY, 217^ if<>0loop] NextByteIn_30      30@ {Length, 6 new bits}
  [do \videoY, 233^ if<>0loop] NextByteIn_30      30@ {Low address}
  [do \videoY, 249^ if<>0loop] NextByteIn_30      30@ {High address}
                               ConditionalCopy_34 34@ {Copy previous bytes}
  [do \nextVideo,   if<>0loop] NextByteIn_30      30@ {Payload 0}
  0 {Delay to force next scanline: "[18 28] 20 30; 18; [20 24" = 158 > 148}
  [do                          ConditionalCopy_34 34@ {Copy previous bytes}
    [do \videoY, 2&  if=0loop] NextByteIn_30      30@ {Payload 1-59}
      \videoY, 238^ if<>0loop]
  [do \videoY, 183^ if<>0loop] NextByteIn_30      30@ {Checksum byte}

  {Plot red or green indicator}
  \sysArgs2, [if=0 12 else 3] A.

  {Check checksum and command, and execute if OK. Also reset checksum.}
  \SYS_ProcessInput_48 48@

  {Advance indicator}
  A<, 11- 127& 12+ A<.
loop]

{
vgaY nextVideo videoY vCPU Comment
---- --------- ------ ---- -------
   0  videoE      238  Yes First vBlank
   1  videoE      179  Yes
   2  videoE      181  Yes
   3  videoE      183  Yes Byte 65 Checksum
  ..  ...         ...  Yes
  14  videoE      205  Yes Byte 0 Protocol ('W')
  ..  ...         ...  Yes
  20  videoE      217  Yes Byte 1 Length (6 bits)
  ..  ...         ...  Yes
  28  videoE      233  Yes Byte 2 Address L
  ..  ...         ...  Yes
  36  videoE      249  Yes Byte 3 Address H
  37  videoE      251  Yes
  38  videoE      253  Yes
  39  videoE      255  Yes
  40  videoE        1  Yes Last vBlank
  41  videoA->B     0   No Pixel
  42  videoB->C     0   No Pixel
  43  videoC->F     0   No Pixel
  44  videoF->A     2  Yes Byte 4 Payload 0
  45  videoA->B     2   No Pixel
  46  videoB->C     2   No Pixel
  47  videoC->F     2   No Pixel
  48  vidoeF->A     4  Yes
  49  videoA->B     4   No Pixel
  50  videoB->C     4   No Pixel
  51  videoC->F     4   No Pixel
  52  videoF->A     6  Yes Byte 5 Payload 1
  ..  ...         ...  ...
 513  videoA->B   236   No Pixel
 514  videoB->C   236   No Pixel
 515  videoC->F   236   No Pixel
 516  videoF->A   238  Yes Byte 64 Payload 58
 517  videoA->B   238   No Pixel
 518  videoB->C   238   No Pixel
 519  videoC->F   238   No Pixel
 520  videoF->E   238  Yes
}

{-----------------------------------------------------------------------+
|                                                                       |
+-----------------------------------------------------------------------}

