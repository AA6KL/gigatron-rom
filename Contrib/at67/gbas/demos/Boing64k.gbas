_runtimePath_ "../runtime"
_runtimeStart_ &hFFFF
_stringWorkArea_ &h69A0 'move string work area for scrolling of text, (defaults to &h7FA0)

'defines the amount of contiguous RAM needed for sprite stripes, (in this case 15*6 + 1)
_spriteStripeChunks_ 15, &h16A0, ascending

'alloc offscreen scrolling area, (<address>, <optional size>, <optional count>, <optional offset>), so that compiler does not try and use it
alloc &h6AA0, 96, 22, &h0100

'volume extinction function, (modelled in Desmos), used to control ADSR
const V_LUT=&h13A0
const V_SIZ=64
def byte(V_LUT, y, 0.0, 0.5, V_SIZ) = 63.0 - exp(-0.5*y)*(1.0-exp(-3.0*y))*1.6125*63.0

'frequency sweep
const F_LUT=&h13E0
const F_SIZ=16
def word(F_LUT, y, 0.0, 360.0, F_SIZ) = sin(y)*200.0 + 1000.0

'maximum string length is 94
conf0$  = "greetings    welcome to gtbasic    this demo shows some of the advanced features that the "
conf1$  = "compiler is capable of    thank you to marcel and walter for making this possible    "
conf2$  = "and now it is time for some quotes whilst you are being mesmerised by boing    "
conf3$  = "if i have seen further than others it is by standing upon the shoulders of giants    "
conf4$  = "what you do not want done to yourself do not do to others    "
conf5$  = "i hear and i forget i see and i remember i do and i understand    "
conf6$  = "he who learns but does not think is lost he who thinks but does not learn is in great danger  "
conf7$  = "to see what is right and not to do it is want of courage or of principle    "
conf8$  = "the superior man is modest in his speech but exceeds in his actions    "
conf9$  = "humility is the solid foundation of all virtues    "
conf10$ = "to see the right and not to do it is cowardice    "
conf11$ = "when you are laboring for others let it be with the same zeal as if it were for yourself    "
conf12$ = "the strength of a nation derives from the integrity of the home    "
conf13$ = "to see and listen to the wicked is already the beginning of wickedness    "
conf14$ = "our greatest glory is not in never falling but in rising every time we fall    "
conf15$ = "everything has beauty but not everyone sees it    "
conf16$ = "the superior man acts before he speaks and afterwards speaks according to his action    "
conf17$ = "learning without thought is labor lost thought without learning is perilous    "
conf18$ = "without feelings of respect what is there to distinguish men from beasts    "
conf19$ = "it is more shameful to distrust our friends than to be deceived by them    "
conf20$ = "an oppressive government is more to be feared than a tiger    "

'poke &h21, peek(&h21) OR 3
sound off

mode 2
set FGBGCOLOUR, &h3F15
cls

set FGCOLOUR, 0
rectf 0, 97, 159 + 96, 119 'clear text scrolling onscreen and offscreen area

const fontStart = 0
load sprite, ../../images/gbas/DemoFont/a.tga, fontStart + 0
load sprite, ../../images/gbas/DemoFont/b.tga, fontStart + 1
load sprite, ../../images/gbas/DemoFont/c.tga, fontStart + 2
load sprite, ../../images/gbas/DemoFont/d.tga, fontStart + 3
load sprite, ../../images/gbas/DemoFont/e.tga, fontStart + 4
load sprite, ../../images/gbas/DemoFont/f.tga, fontStart + 5
load sprite, ../../images/gbas/DemoFont/g.tga, fontStart + 6
load sprite, ../../images/gbas/DemoFont/h.tga, fontStart + 7
load sprite, ../../images/gbas/DemoFont/i.tga, fontStart + 8
load sprite, ../../images/gbas/DemoFont/j.tga, fontStart + 9
load sprite, ../../images/gbas/DemoFont/k.tga, fontStart + 10
load sprite, ../../images/gbas/DemoFont/l.tga, fontStart + 11
load sprite, ../../images/gbas/DemoFont/m.tga, fontStart + 12
load sprite, ../../images/gbas/DemoFont/n.tga, fontStart + 13
load sprite, ../../images/gbas/DemoFont/o.tga, fontStart + 14
load sprite, ../../images/gbas/DemoFont/p.tga, fontStart + 15
load sprite, ../../images/gbas/DemoFont/q.tga, fontStart + 16
load sprite, ../../images/gbas/DemoFont/r.tga, fontStart + 17
load sprite, ../../images/gbas/DemoFont/s.tga, fontStart + 18
load sprite, ../../images/gbas/DemoFont/t.tga, fontStart + 19
load sprite, ../../images/gbas/DemoFont/u.tga, fontStart + 20
load sprite, ../../images/gbas/DemoFont/v.tga, fontStart + 21
load sprite, ../../images/gbas/DemoFont/w.tga, fontStart + 22
load sprite, ../../images/gbas/DemoFont/x.tga, fontStart + 23
load sprite, ../../images/gbas/DemoFont/y.tga, fontStart + 24
load sprite, ../../images/gbas/DemoFont/z.tga, fontStart + 25
load sprite, ../../images/gbas/DemoFont/space.tga, fontStart + 26

const boingStart = fontStart + 27
load sprite, ../../images/gbas/Boing/Boing0.tga, boingStart + 0
load sprite, ../../images/gbas/Boing/Boing1.tga, boingStart + 1
load sprite, ../../images/gbas/Boing/Boing2.tga, boingStart + 2
load sprite, ../../images/gbas/Boing/Boing3.tga, boingStart + 3
load sprite, ../../images/gbas/Boing/Boing4.tga, boingStart + 4
load sprite, ../../images/gbas/Boing/Boing5.tga, boingStart + 5
load sprite, ../../images/gbas/Boing/Boing6.tga, boingStart + 6
load sprite, ../../images/gbas/Boing/Boing7.tga, boingStart + 7
load sprite, ../../images/gbas/Boing/Boing8.tga, boingStart + 8
load sprite, ../../images/gbas/Boing/Boing9.tga, boingStart + 9
load sprite, ../../images/gbas/Boing/Boing10.tga, boingStart + 10
load sprite, ../../images/gbas/Boing/Boing11.tga, boingStart + 11
load sprite, ../../images/gbas/Boing/Boing12.tga, boingStart + 12
load sprite, ../../images/gbas/Boing/Boing13.tga, boingStart + 13

'i and l = 6, m and w = 18, space = 6
'static initialisation of the array produces no code, always attempt this first
dim fontWidths(26) = 12, 12, 12, 12, 12, 12, 12, 12, 6, 12, 12, 6, 18, 12, 12, 12, 12, 12, 12, 12, 12, 12, 18, 12, 12, 12, 6

'can't have an array of strings yet, so manually construct one, (+ 1 skips past the length byte)
'static initialisation of the array produces no code, always attempt this first
dim quoteAddrs(20) = @conf0$+1, @conf1$+1, @conf2$+1, @conf3$+1, @conf4$+1, @conf5$+1, @conf6$+1, @conf7$+1, @conf8$+1, @conf9$+1, @conf10$+1, @conf11$+1, @conf12$+1, @conf13$+1, @conf14$+1, @conf15$+1, @conf16$+1, @conf17$+1, @conf18$+1, @conf19$+1, @conf20$+1

const g = 4
f = boingStart : y = &h0100 : text = 160 : fstep = 1 : xstep = 100
x = 0 : ystep = x : scrl = ystep : char = scrl : stamp = char : width = stamp : quote = width : crash = quote : time = crash

loop:
    set SOUNDTIMER, 2
    
    sprite noFlip, f, x.hi, y.hi
    
    'boing frame
    f = f + fstep
    if f &&> boingStart + 13 then f = boingStart
    if f &&< boingStart then f = boingStart + 13
  
    'boing xpos
    x = x + xstep
    if x.hi &> 111 'performs the following check if (x.hi > 111) or (x.hi < 0)
        xstep = -xstep : x = xstep + x
        fstep = -fstep
        crash = 1
        time = 0
    endif
    
    'boing ypos
    ystep = ystep + g
    y = y + ystep
    if y.hi &> 56
        y.hi = 56
        ystep = -ystep
        crash = 1
        time = 0
    endif
    
    'collision sound
    if crash &= 1
        gosub crashSound
    endif
    
    'print character one at a time offscreen
    gosub printText
    
    'scrolling text
    poke &h01C5, scrl
    inc scrl
goto loop


printText:
    if stamp &&= 0
getChar:    
        c = peek(quoteAddrs(quote) + char)
        inc char
        
        if c &&= 0
            char = 0
            inc quote
            if quote &&= 21
                quote = 0
            endif
            goto getChar
        elseif c &&= 32
            c = 26
        else
            c = c - 97
        endif
        
        sprite noFlip, fontStart + c, text, 98
        
        width = fontWidths(c)
        text = text + width
    endif
    
    inc stamp
    if stamp &&= width
        stamp = 0
    endif
return

crashSound:
    volume = peek(V_LUT + (time AND (V_SIZ-1)))
    freq = deek(F_LUT + ((time AND (F_SIZ-1)) LSL 1))
    foffs = 0
    for i=1 to 4
        sound on, i, freq + foffs, volume, 0
        foffs = foffs + 100
    next i

    inc time
    if time &= 64
        time = 0 : crash = 0
    endif
return
