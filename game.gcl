
{---------- Page 4 ----------}
$0400:

{120 frames startup delay (with sound) to wait for VGA monitor}

120 $12! {soundTimer}
[do $12? if<>0loop]

{Test of built-in font}
62 P= {Pen color}
[do
  $0700 C= {Text} {XXX: define a ROM string and get a pointer to it}
  $0500 F= {Font data}
  $0800 S= {Pen position at top of screen}
  $1000 U= {Next line (to stop the inner drawing loop)}

  [do
    C; D= {D is character to be printed}
    if<>0

    D 82- [if<0 50+ E= $0500 else E= $0600] F=
    E E+ T= T+ E+ {Multiply by 5}
    F+ F= {Add to font page start}
    {Print character}
    5 I= [do
      I [if<>0 F; else 0] A=
      S T=
      {Print vertical slice}
      [do
        A 128& [if=0 0 {Black} T! else P {Pen color} T!]
        A A+ A=
        T>++
        T U- if<0loop]
      F 1+ F=
      S 1+ S=
      I 1- I= if>=0loop]
    C<++ loop]
  P 8^ if<>0 P 9- P= loop]

{Scroller game, might become Zaxxon or Flappy Bird}

$0111 S= {X shift starting from line 8}

{Random}
0 Q= R=
J=

{Ball acceleration E, height Y and velocity Z}
E=
Z=
G= {Saved background}
$1800 Y= B=

{Obstacle height}
90 H=

$0500 call
{---------- Page 5 ----------}
$0500:

[do
  {Get X from scanline table}
  S? X=

  {Enable gravity after a while}
  120^ [if=0 10 E=]

  {Stupid RNG}
  S? R+ Q+ R=
  Q+ [if<0 43^] Q+ Q+ Q=

  {Random height adjustment}
  H 88-  [if>0 R  16& if<>0 H 1- H=]
  H 118- [if<0 R 128& if<>0 H 1+ H=]

  {J=($08+H)<<8}
  H $08+ J>!

  {X drawing position is 1 pixel outside the visible area}
  X 160+ 255& T=

  {Clear vertical line}
  $1000 T+ V=
  [do 1 V!
    V>++
    V if>0loop]

  {Star}
  R 127& A= A 111- [if>0 119 A=]
  A 16+ A>! T A<!
  2 {Red} A!

  {Draw vertical line to bottom of screen}
  J T+ V=

  63 V! {First pixel white}
  V>++
  X 8& H+ C=                          {Begin of checkerboard}
  [do [C 1+ C= 8& if=0 42 else 32] V! {Checkerboard colors}
      V>++
      V if>0loop]

  $0600 call
loop]

{---------- Page 6 ----------}
$0600:

  {Wait for vertical blank}
  [do 8? if<>0loop]

  {Scroll screen 1 pixel}
  X 1+ S!

  {Clear ball}
  G B!

  {Update ball position and velocity}
  E Z+ Z= {Update velocity with gravity}
  Y Z+ Y= {Update height with velocity}

  {Check new ball position}
  X 50+ B=
  Y>? B>!
  B? G=

  {Reverse if we're hitting something}
  G 2- [if>0
    0 Z- Z=
    Y Z+ Y=
    10 $12! {1/6th second of sound}

    X 50+ B=
    Y>? B>!
    B? G=
  ]

  {Draw the ball}
  63 {White} B!

  return

{---------- End ----------}

{
  {Vladimir Vassilevsky's LCG
   seed = (seed << 7) - seed + 251; // 127*seed+251
   return (u8)(seed + (seed>>8));
  }
  Q Q+ R= R+ R= R+ R= R+
    R= R+ R= R+ R= R+ Q- R=
  251 R+ Q=
  Q>? R+ R=
}

