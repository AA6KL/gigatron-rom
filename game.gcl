
{---------- Page 4 ----------}
$0400:

{120 frames startup delay (with sound) to wait for VGA monitor}

120 $12! {soundTimer}
[do $12? if<>0loop]

{Test of built-in font}
62 P= {Pen color}
[do
  $0400 F= {Font data}
  $0800 S= {Top of screen}
  $1000 U=
  [do
    F; A=
    S T=
    [do
      A 128& [if=0 0 {Black} T! else P {Pen color} T!]
      A A+ A=
      $0100 T+ T=
      U- if<0loop]
    1 F+ F=
    1 S+ S=
    255& 160^ if<>0loop]
  P 8^ if<>0 -9 P+ P= loop]

{Scroller game, might become Zaxxon or Flappy Bird}

$0111 S= {X shift starting from line 8}

{Random}
0 Q= R=
J=

{Ball acceleration E, height Y and velocity Z}
E=
Z=
G= {Saved background}
$1800 Y= B=

{Obstacle height}
90 H=

$0500*
{---------- Page 5 ----------}
$0500:

[do
  {Get X from scanline table}
  S? X=

  {Enable gravity after a while}
  120^ [if=0 10 E=]

  {Stupid RNG}
  S? R+ Q+ R=
  Q+ [if<0 43^] Q+ Q+ Q=

  {Random height adjustment}
  -60 H+ [if>0 R 16& if<>0 -1 H+ H=]
  -90 H+ [if<0 R 128& if<>0 1 H+ H=]

  {J=($08+H)<<8}
  $08 H+ J>!

  {X drawing position is 1 pixel outside the visible area}
  160 X+ 255& T=

  {Clear vertical line}
  $1000 T+ V=
  [do V! 
    $100 V+ V=
    if>0loop]

  {Star}
  R 127& A= -111 A+ [if>0 119 A=]
  16 A+ A>! T A<!
  2 {Red} A!

  {Draw vertical line to bottom of screen}
  J T+ V=

  63 V! {First pixel white}
  $100 V+ V=
  X 8& H+ C=                          {Begin of checkerboard}
  [do [1 C+ C= 8& if=0 42 else 32] V! {Checkerboard colors}
      $100 V+ V=
      if>0loop]

  $0600*
loop]

{---------- Page 6 ----------}
$0600:

  {Wait for vertical blank}
  [do 8? if<>0loop]

  {Scroll screen 1 pixel}
  1 X+ S!

  {Clear ball}
  G B!

  {Update ball position and velocity}
  E Z+ Z= {Update velocity with gravity}
  Y Z+ Y= {Update height with speed}

  {Check new ball position}
  50 X+ B=
  Y>? B>!
  B? G=

  {Reverse if we're hitting something}
  2 G- [if<0
    0 Z- Z=
    Y Z+ Y=
    10 $12! {1/6th second of sound}

    50 X+ B=
    Y>? B>!
    B? G=
  ]

  {Draw the ball}
  63 {White} B!

  return

{---------- End ----------}

{
  {Vladimir Vassilevsky's LCG
   seed = (seed << 7) - seed + 251; // 127*seed+251
   return (u8)(seed + (seed>>8));
  }
  Q Q+ R= R+ R= R+ R= R+
    R= R+ R= R+ R= R+ Q- R=
  251 R+ Q=
  Q>? R+ R=
}

