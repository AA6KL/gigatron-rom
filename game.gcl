
{---------- Page 4 ----------}
$0400:

{Startup delay}
10 D= [do -1 D+ D= if<>0loop]

{ Test of built-in font }

$0505 F= {Font data}
$0800 S= {Top of screen}
$1000 U=
[do
  F; A=
  S T=
  [do
    A 128& [if=0 0 {Black} T! else 8 {Green} T!]
    A A+ A=
    $0100 T+ T=
    U- if<0loop]
  1 F+ F=
  1 S+ S=
  255& 160^ if<>0loop]

{Delay}
{0 D= [do -1 D+ D= if<>0loop]}

$0011 W= {Sound timer address in zero page}

{Scroller game, might become Zaxxon or Flappy Bird}

$0111 S= {X shift starting from line 8}

{Random}
0 Q= R=
J=

{Ball acceleration D, height Y and velocity Z}
D=
Z=
$2800 Y= B=

{Obstacle height}
90 H=

$05*
{---------- Page 5 ----------}
$0500:

[
  {Get X from scanline table}
  S? X=

  {Start gravity after a while}
  {160^ [if<>0 10 D=]}

  {Stupid RNG}
  S? R+ Q+ R=
  Q+ [if<0 43^] Q+ Q+ Q=

{
  {Random height adjustment}
  -60 H+ [if>0 R 16& if<>0 -1 H+ H=]
  -90 H+ [if<0 R 128& if<>0 1 H+ H=]
}

  {J=($08+H)<<8}
  $08 H+ J>!

  {X drawing position is 1 pixel outside the visible area}
  160 X+ 255& T=

  {Clear vertical line}
  $1000 T+ V=
  [do V! 
    $100 V+ V=
    if>0loop]

  {Star}
  R 127& A= -111 A+ [if>0 119 A=]
  16 A+ A>! T A<!
  2 {Red} A!

  {Draw vertical line to bottom of screen}
  J T+ V=

  63 V! {First pixel white}
  $100 V+ V=
  X 8& H+ C=                         {Begin of checkerboard}
  [do {[1 C+ C= 8& if=0 42 else 32] V! {Checkerboard colors}}
      48 V! {Blue}
      $100 V+ V=
      if>0loop]

  {Update ball position and velocity}
  [-3 G+ if<=0
    Y Z+ Y= if>=0 {skip}
  else {G<>1 or Y+Z<0}
    0 Z- Z=
    Y+ Y= {Reverse if hitting the bottom}
    12 W! {1/5th second of sound}
  ]
  {$1400 Y- [if>0 $1400 Y=] {Clip}}
  D Z+ Z=

  {Wait for vertical blank}
  [do 8? if<>0loop]

  {Scroll screen 1 pixel}
  1 X+ S!

  {Clear ball}
  G B!

{
$06*
{---------- Page 5 ----------}
$0600:
}

  {Draw the ball}
  50 X+ B=
  Y>? B>!
  B? G=
  63 {White} B!

$05*
]

{---------- End ----------}

{
  {Vladimir Vassilevsky's LCG
   seed = (seed << 7) - seed + 251; // 127*seed+251
   return (u8)(seed + (seed>>8));
  }
  Q Q+ R= R+ R= R+ R= R+
    R= R+ R= R+ R= R+ Q- R=
  251 R+ Q=
  Q>? R+ R=
}

