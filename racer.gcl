
{-----------------------------------------------------------------------+
|                                                                       |
|       Racer game                                                      |
|                                                                       |
+-----------------------------------------------------------------------}

gcl0x

{
XXX Randomized trajectory
XXX Bending road
XXX Engine sound
XXX Slower acceleration
XXX Breaking and automatic slow-down to cruising speed

XXX Draw skyline
XXX Scroll skyline

XXX Checkerboard grass?
XXX Obstacles?
XXX Finish line?
XXX Crash sound effect?
XXX Sprite acceleration?
}

{-----------------------------------------------------------------------+
|                       RAM page 3                                      |
+-----------------------------------------------------------------------}

[def {
  DrawChar -- Pos Color Char

  Draw a 5x8 character on screen with the built-in font.
  `Char' must be in the 32-127 range (this is not checked)
}
  {Map ASCII code to offset in font table}
  Char 82- [if<0 50+ i= \font32up
            else     i= \font82up] fontData= {Select low or high page}
  i i+ tmp= tmp+ i+    {Multiply by 5}
  fontData+ fontData=  {Add to page address to reach bitmap data for Char}
  $800 Pos+ q=         {Where to stop the inner drawing loop}

  {Draw 6 vertical slices: 5 using font data, the last with all-zeros}
  5 i= [do
    [if<>0 fontData 0; fontData<++ else 0] bits=
    Pos p=
    {Draw vertical slice}
    [do
      bits 128& [if=0 BgColor else Color] p!
      bits bits+ bits=
      p>++
      p q- if<0loop]
    Pos<++
    i 1- i= if>=0loop]
  ret
] DrawChar=

[def {
  DrawText(Text,Pos)

  Draw a zero-terminated text string to the screen.
  Character 10 acts as newline.
  There is no check for running off screen.
}
  push
  [do
    Text? Char= {Next character to be printed}
    if<>0       {Zero termination}
      Text<++   {Advance text pointer}
      Char 10^ [if=0 Pos<! $800 Pos+ Pos=
                else DrawChar call]
      loop]
  pop call
] DrawText=

[def
   $47. $69. $67. $61. $74. $72. $6f. $6e. 0. {Gigatron}
] Title=

[def {
  Wait -- Wait Delay number of frames (range 1..255)
}
  \frameCount? Delay+ 255& tmp=
  [do \frameCount? tmp- if<>0loop]
  ret
] Wait=

[def { ExtractDigit -- Value Radix }
  $30 Char=
  Value Radix- [if>=0
    [do
      Value=
      Char<++
      Radix-
      if>=0loop]
  ]
  ret
] ExtractDigit=

{-----------------------------------------------------------------------+
|}\vLR>++ ret{          RAM page 4                                      |
+-----------------------------------------------------------------------}
$0400:

[def {
  ClearScreen -- Clear screen from current position to bottom right
}
  Pos p=
  Pos 255| 255-         {XXX Is there a better way to clear the low byte?}
  i= $8001 i+ i=        {Offset to move to next stripe}
  [do
    {Clear one stripe}
    p [do BgColor p! $100 p+ p= if>=0loop]
    {Next stripe}
    i+ p=
    255& 160^ if<>0loop]
  ret
] ClearScreen=

[def {
  DrawPixels -- Write a single line of pixels on two correspinding
                road scanlines (light and dark). Remove previous
                pixels, if any.
                XXX This is probably better done as a SYS call
}
  { Setup page reference for write pointers }
  Video? 254&  p>! 1| q>! Video<++

  {Find segment to clear from position p[0] to position q[0]}
  0 p<! q<!
  p? i= q? i- i=        {i = q[0] - p[0]}
  p? p<! q<!            {p,q = p+p[0], q+p[0]}

  {Clear previous object}
  {XXX Only clear pixels outside new area?}
  [do
    21 p! q!            {Clear pixel on both lines}
    p<++ q<++
    i 1- i=
    if>0loop]

  {Set low bytes of p and q, to turn them into pixel write pointers}
  Sprite s=             {Pixel read pointer}
  0 p<!                 {First let p point to start of page}
  Video? X+ X= s? X+    {First byte in pixel data is offset}
  p!                    {Remember starting point}
  p<! q<!               {Set low bytes}

  {Draw actual pixels}
  s<++
  [do
     p? 21^ Collision+ Collision=
     s?
     p! p<++
     q! q<++
     s<++ s? if<>0loop]

  {Remember end point for later removal}
  0 q<! p<? q!

  Video 3+ Video=
  ret
] DrawPixels=

{
  Car sprite. Black is 0 but also the teminator, therfore use 64 for black.
  XXX Reduce footprint by bringing all lines under one 'def'
}
[def 2.         64. 64. 64. 40. 60. 60. 40. 64. 64. 64.         0.] Car0=
[def 3.             40. 20. 20. 63. 63. 20. 20. 40.             0.] Car1=
[def 2.         40. 20. 20. 20. 40. 40. 20. 20. 20. 40.         0.] Car2=

{-----------------------------------------------------------------------+
|}\vLR>++ ret{          RAM page 5                                      |
+-----------------------------------------------------------------------}
$0500:

[def 0. 64. 64. 64. 64. 40. 20. 20. 20. 20. 40. 64. 64. 64. 64. 0.] Car3=
[def 0. 64. 64. 64. 64. 21. 21. 21. 21. 21. 21. 64. 64. 64. 64. 0.] Car4=

[def
  push
  $2080 Pos=

  1 Width=
  [do
    3 {Bright Red} CurbColor=
    8 {Green} GrassColor=
    SetupSegment call
    Pos>++

    63 {White} CurbColor=
    12 {Bright Green} GrassColor=
    SetupSegment call
    Pos>++

    Width<++
    Pos if>=0loop]

  {Setup undo infomation for last lines (for car sprite)}
  $7400 [do \POKE. \vAC. \vAC>++ if>0loop]

  pop call
] SetupRoad=

[def
  {Calculate CurbLeft}
  {XXX Simplify}
  Width Width+ tmp= {x2}
          tmp+ tmp= {x4}
          tmp+ tmp= {x8}
          tmp+ tmp= {x16}
          tmp+ tmp= {x32}
  tmp>? CurbLeft=   {CurbLeft = Width/16}
  Width CurbLeft- CurbRight=

  Pos p=

  0 CurbRight- W=
  {Draw road segment}
  Width i= [do
    W 1+ W= [if<=0 21 {Dark Grey} else CurbColor] p!
    p<++
    i 1- i= if>0loop]

  {Draw grass}
  [do
    GrassColor p!
    p<++
    p<? if<>0loop]

  {Draw mirror image}
  [do
    p 255^ peek p!
    p<++
    p<? 128^ if<>0loop]

  ret
] SetupSegment=

{-----------------------------------------------------------------------+
|}\vLR>++ ret{          RAM page 6                                      |
+-----------------------------------------------------------------------}
$0600:

[def {
  Play a single game until the end
}
  push

  {Run game loop}
  [do
    [do \vBlank? if=0loop]
    AdvanceCar call

    {See if we have reached the finish}
    Distance
    [if<0
      Time BestTime- [if<0 Time BestTime=]

      4 Dig0= Dig1= Dig2=
      [do tmp=
        BestTime Value= $87c Pos= DrawTime
        tmp 1- if>0loop]

      {New lap}
      0 Time=
      $7400 Distance=
    ]
    128& [if=0 Distance else Distance 255^] 255& 64- DX=
    DX+ DX= DriftX= DX+ DX= DX+ DX= DX+ DX= {Shift 4}

    DrawCurvature call

    {Control car}
    Speed [do
      tmp=
      CarX DriftX- CarX= {Drift is proportional to speed}
      tmp 1- if>0loop]
    0 Steer=
    \serialRaw? $01& {~buttonRight} [if=0  $200 CarX+ CarX= +1 Steer=]
    \serialRaw? $02& {~buttonLeft}  [if=0 -$200 CarX+ CarX= -1 Steer=]
    \serialRaw? $40& {~buttonA}     [if=0 Speed 4- [if<0 Speed<++]]
    \serialRaw? $80& {~buttonB}     [if=0 Speed 1- [if>0 Speed=]]

    {Draw car}
    0 Collision=
    DrawRaceCar call

    DrawPerspective call

    {Draw current time}
    {XXX Do this incrementally}
    Time Value=
    $800 Pos=
    63 Color=
    DrawTime call

    Collision if=0loop]
    pop call
] PlayGame=

{-----------------------------------------------------------------------+
|}\vLR>++ ret{          RAM page 7                                      |
+-----------------------------------------------------------------------}
$0700:

[def {
  DrawTime -- Render elapsed time (in frams) as M:SS.s -- Value Radix
              Draw no more than 1 digit at a time
}
  push

  3600 Radix= ExtractDigit call {\sysArgs7? $30+ Char= {Show frame speed for debugging}}
                                Dig2 Char- [if<>0 Char Dig2= DrawChar call else Pos 12+ Pos=
   600 Radix= ExtractDigit call Dig1 Char- [if<>0 Char Dig1= DrawChar call else Pos  6+ Pos=
    60 Radix= ExtractDigit call Dig0 Char- [if<>0 Char Dig0= DrawChar call else Pos 12+ Pos=
     6 Radix= ExtractDigit call DrawChar call
  ]]]
  pop call
] DrawTime=

[def
  {Step 1: Calculate road curvature}

  0 X=
  $74d0 p= {array[48] on $74d0-$74ff, in display memory but out of view}
  [do
    X>? p! p<++
    X DX+ X=
    p<? if<>0loop]

  {Step 2: Update video table low bytes for road}
  {This step is most timing-sensitive wrt. the video loop}

  $74fa peek 48- LastX= {Pivot 12px from bottom / front of car}
  $0131 p=              {Video table start of road}

  $74d0 q=
  [do
    q? LastX- p!
    p<++ p<++ p<++ p<++
    {p 4+ p=}

    q? LastX=
    q<++
    q<? if<>0loop]
  ret
] DrawCurvature=

{-----------------------------------------------------------------------+
|}$08a0 call{           RAM page 8                                      |
+-----------------------------------------------------------------------}
$08a0:

[def
  {Update video table high bytes for road width and color scheme}
  {We do this part last because any timing delays here don't really
   cause visible problems.}

  $20 SegmentY=         {Start with smallest segment}
  $0130 p=              {Video table top of road, at horizon}

  \invTable q=
  [do
    q 20;               {Y to Z depth mapping for perspective. The offset
                         of 20 just reduces the aliasing near horizon.}
    Distance+ 4&        {Vertical scrolling Distance}
    [if<>0 1] tmp=
    SegmentY 254& tmp+ p!
    p<++ p<++
    SegmentY<++
    q<++

    p<? 240^ if<>0loop]

  ret
] DrawPerspective=

{-----------------------------------------------------------------------+
|}$09a0 call{           RAM page 9                                      |
+-----------------------------------------------------------------------}
$09a0:

[def
  {Update time with actual elapsed frames}
  \frameCount? LastFrame- 255& \sysArgs7! Time+ [if<0 $7fff] Time=
  \frameCount? LastFrame=

  {Advance car along track}
  Speed Distance+ Distance=

  {Update progress indicator}
  $0f1f p= Distance>? p+ p= 21 p! p<++ 60 p!
  ret
] AdvanceCar=

{-----------------------------------------------------------------------+
|}$0aa0 call{           RAM page 10                                     |
+-----------------------------------------------------------------------}
$0aa0:

[def
  push
  $01d9 peek 255^ X=
  CarX>? X+ X=
  $01d8 Video=
  Car0 Sprite= DrawPixels call
  Car1 Sprite= DrawPixels call
  X Steer- X=
  Car2 Sprite= DrawPixels call
  Car3 Sprite= DrawPixels call
  Car4 Sprite= DrawPixels call
  pop call
] DrawRaceCar=

{-----------------------------------------------------------------------+
|}$0ba0 call{           RAM page 11                                     |
+-----------------------------------------------------------------------}
$0ba0:

[def {
  Intro
}
  push

  {Display welcome tekst}
  48 BgColor=
  $800 Pos= ClearScreen call
  63 Color=
            EmptyTime Text= DrawText call
  $87c Pos= EmptyTime Text= DrawText call
  $838 Pos= Title Text= DrawText call
  60 Delay= Wait call

  pop call
] Intro=
  [def $2d. $3a. $2d. $2d. $2e. $2d. 0.] EmptyTime=

{-----------------------------------------------------------------------+
|}$0ca0 call{           RAM page 12                                     |
+-----------------------------------------------------------------------}
$0ca0:

{-----------------------------------------------------------------------+
|}$0da0 call{           RAM page 13                                     |
+-----------------------------------------------------------------------}
$0da0:

{-----------------------------------------------------------------------+
|}$0ea0 call{           RAM page 14                                     |
+-----------------------------------------------------------------------}
$0ea0:

{-----------------------------------------------------------------------+
|}$0fa0 call{           RAM page 15                                     |
+-----------------------------------------------------------------------}
$0fa0:

{--- Run ---}
Intro call

{--- Main loop ---}
[do
  {Setup new game}
  0 DX= DrawCurvature call
  DrawPerspective call
  SetupRoad call
  $7400 Distance=
  $7fff BestTime=
  0 Time= Value=
  \frameCount? LastFrame=
  $7900 CarX=
  1 Speed=

  {XXX "Get ready" message}

  {Play game until finished}
  PlayGame call

  {"Game over" message}
  $1435 Pos=
  [def
    $47. $41. $4d. $45. $20. $4f. $56. $45. $52. 0. {GAME OVER}
  ] Text=
  15 Color=
  DrawText call

  240 Delay= Wait call

  $1000 Pos= ClearScreen call
 loop]

{-----------------------------------------------------------------------+
|       End                                                             |
+-----------------------------------------------------------------------}

